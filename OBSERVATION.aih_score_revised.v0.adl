archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.aih_score_revised.v0

concept
	[at0000]	-- Revised original score for Autoimmune Hepatitis (AIH)
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Maryam Razavi">
		["email"] = <"maryam.razavi@cambio.se">
		["organisation"] = <"Cambio CDS">
		["date"] = <"2021-02-03">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the results for each component parameter and the revised original score for Autoimmune Hepatitis (AIH).">
			use = <"Use to record the results for each component parameter and the revised original score for Autoimmune Hepatitis (AIH).">
			keywords = <"autoimmune", "hepatitis", "AIH", "biopsy">
			misuse = <"">
			copyright = <"© Cambio CDS">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <>
	other_details = <
		["references"] = <"Alvarez F, Berg PA, Bianchi FB, Bianchi L, Burroughs AK, Cancado EL, Chapman RW, Cooksley WG, Czaja AJ, Desmet VJ, Donaldson PT, Eddleston AL, Fainboim L, Heathcote J, Homberg JC, Hoofnagle JH, Kakumu S, Krawitt EL, Mackay IR, MacSween RN, Maddrey WC, Manns MP, McFarlane IG, Meyer zum Büschenfelde KH, Zeniya M, et al. International Autoimmune Hepatitis Group Report: review of criteria for diagnosis of autoimmune hepatitis. J Hepatol. 1999 Nov;31(5):929-38. doi: 10.1016/s0168-8278(99)80297-9. PMID: 10580593.

Czaja AJ. Performance parameters of the diagnostic scoring systems for autoimmune hepatitis. Hepatology. 2008 Nov;48(5):1540-8. doi: 10.1002/hep.22513. PMID: 18924244.">
		["current_contact"] = <"maryam.razavi@cambio.se">
		["MD5-CAM-1.0.1"] = <"70928F9247094E73A703825E71FEB501">
	>

definition
	OBSERVATION[at0000] matches {	-- Revised original score for Autoimmune Hepatitis (AIH)
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Sex
										value matches {
											0|[local::at0005], 	-- Male 
											2|[local::at0006]  	-- Female
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {	-- ALP:AST or ALP:ALT ratio
										value matches {
											-2|[local::at0010], 	-- >3.0
											0|[local::at0009], 	-- 1.5-3.0
											2|[local::at0008]  	-- <1.5
										}
									}
									ELEMENT[at0011] occurrences matches {0..1} matches {	-- Serum globulins or IgG
										value matches {
											0|[local::at0012], 	-- <1.0
											1|[local::at0013], 	-- 1.0-1.5
											2|[local::at0014], 	-- 1.5-2.0
											3|[local::at0015]  	-- >2.0
										}
									}
									ELEMENT[at0016] occurrences matches {0..1} matches {	-- ANA, SMA, or LKM-1 antibody
										value matches {
											0|[local::at0017], 	-- <1:40
											1|[local::at0018], 	-- 1:40
											2|[local::at0019], 	-- 1:80
											3|[local::at0020]  	-- >1:80
										}
									}
									ELEMENT[at0056] occurrences matches {0..1} matches {	-- Optional additional parameters
										value matches {
											0|[local::at0057], 	-- None of the above
											1|[local::at0058], 	-- HLA-DR3 or HLA-DR4
											2|[local::at0059]  	-- Seropositivity for other defined autoantibodies
										}
									}
									ELEMENT[at0021] occurrences matches {0..1} matches {	-- AMA
										value matches {
											-4|[local::at0023], 	-- Positive
											0|[local::at0022]  	-- Negative
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Hepatitis viral markers
										value matches {
											-3|[local::at0023], 	-- Positive
											3|[local::at0022]  	-- Negative
										}
									}
									ELEMENT[at0027] occurrences matches {0..1} matches {	-- Recent or current use of hepatotoxic drugs
										value matches {
											-4|[local::at0029], 	-- Yes
											1|[local::at0028]  	-- No
										}
									}
									ELEMENT[at0030] occurrences matches {0..1} matches {	-- Average alcohol intake
										value matches {
											-2|[local::at0033], 	-- >60 g/day
											0|[local::at0032], 	-- 25-60 g/day
											2|[local::at0031]  	-- <25 g/day
										}
									}
									ELEMENT[at0034] occurrences matches {0..1} matches {	-- Liver histology: interface hepatitis
										value matches {
											0|[local::at0029], 	-- Yes
											3|[local::at0028]  	-- No
										}
									}
									ELEMENT[at0037] occurrences matches {0..1} matches {	-- Liver histology: predominantly lymphoplasmacytic infiltrate
										value matches {
											0|[local::at0029], 	-- Yes
											1|[local::at0028]  	-- No
										}
									}
									ELEMENT[at0040] occurrences matches {0..1} matches {	-- Liver histology: rosetting of liver cells
										value matches {
											0|[local::at0028], 	-- No
											1|[local::at0029]  	-- Yes
										}
									}
									ELEMENT[at0043] occurrences matches {0..1} matches {	-- Liver histology: biliary changes
										value matches {
											-3|[local::at0029], 	-- Yes
											0|[local::at0028]  	-- No
										}
									}
									ELEMENT[at0046] occurrences matches {0..1} matches {	-- Liver histology: other changes
										value matches {
											-3|[local::at0029], 	-- Yes
											0|[local::at0028]  	-- No
										}
									}
									ELEMENT[at0049] occurrences matches {0..1} matches {	-- Other autoimmune disease(s)
										value matches {
											0|[local::at0028], 	-- No
											2|[local::at0029]  	-- Yes
										}
									}
									ELEMENT[at0052] occurrences matches {0..1} matches {	-- Response to therapy
										value matches {
											2|[local::at0053], 	-- Complete
											3|[local::at0054]  	-- Relapse
										}
									}
									ELEMENT[at0055] occurrences matches {0..1} matches {	-- Total score
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::380]>
												list = <
													["1"] = <
														units = <"1">
														magnitude = <|-20.0..26.0|>
													>
												>
											>
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0060] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0062] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Revised original score for Autoimmune Hepatitis (AIH)">
					description = <"An assessment score used to diagnose autoimmune hepatitis.">
					comment = <"Original version, c 1999">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Sex">
					description = <"">
				>
				["at0005"] = <
					text = <"Male ">
					description = <"">
				>
				["at0006"] = <
					text = <"Female">
					description = <"">
				>
				["at0007"] = <
					text = <"ALP:AST or ALP:ALT ratio">
					description = <"Alkaline Phosphatase (ALP), aspartate transaminase (AST), alanine aminotransferase (ALT). The ALP:AST (or ALT) ratio relates to the degree of elevation 
above upper normal limits (unl) of these enzymes, i.e.=(IU/l 
ALP+unl ALP)+(IU/l AST+unl AST).">
				>
				["at0008"] = <
					text = <"<1.5">
					description = <"">
				>
				["at0009"] = <
					text = <"1.5-3.0">
					description = <"">
				>
				["at0010"] = <
					text = <">3.0">
					description = <"">
				>
				["at0011"] = <
					text = <"Serum globulins or IgG">
					description = <"Times above upper limit of normal.">
				>
				["at0012"] = <
					text = <"<1.0">
					description = <"">
				>
				["at0013"] = <
					text = <"1.0-1.5">
					description = <"">
				>
				["at0014"] = <
					text = <"1.5-2.0">
					description = <"">
				>
				["at0015"] = <
					text = <">2.0">
					description = <"">
				>
				["at0016"] = <
					text = <"ANA, SMA, or LKM-1 antibody">
					description = <"Antinuclear Antibody (ANA), Smooth Muscle Antibody (SMA) , Liver kidney microsome type 1 (anti-LKM-1). Titres determined by indirect immunofluorescence on rodent 
tissues or, for ANA, on HEp-2 cells. Lower titres (especially of 
LKM-1) are significant in children and should be scored at least 
+1. ">
				>
				["at0017"] = <
					text = <"<1:40">
					description = <"">
				>
				["at0018"] = <
					text = <"1:40">
					description = <"">
				>
				["at0019"] = <
					text = <"1:80">
					description = <"">
				>
				["at0020"] = <
					text = <">1:80">
					description = <"">
				>
				["at0021"] = <
					text = <"AMA">
					description = <"Antimitochondrial Antibody (AMA).">
				>
				["at0022"] = <
					text = <"Negative">
					description = <"">
				>
				["at0023"] = <
					text = <"Positive">
					description = <"">
				>
				["at0024"] = <
					text = <"Hepatitis viral markers">
					description = <"Hepatitis A, B and C viruses (i.e., positive/negative for IgM anti-HAV, HBsAg, IgM anti-HBc, anti-HCV and HCV-RNA); if all are negative and viral etiology is still suspected, CMV/EBV testing may be relevant.">
				>
				["at0027"] = <
					text = <"Recent or current use of hepatotoxic drugs">
					description = <"">
				>
				["at0028"] = <
					text = <"No">
					description = <"">
				>
				["at0029"] = <
					text = <"Yes">
					description = <"">
				>
				["at0030"] = <
					text = <"Average alcohol intake">
					description = <"">
				>
				["at0031"] = <
					text = <"<25 g/day">
					description = <"">
				>
				["at0032"] = <
					text = <"25-60 g/day">
					description = <"">
				>
				["at0033"] = <
					text = <">60 g/day">
					description = <"">
				>
				["at0034"] = <
					text = <"Liver histology: interface hepatitis">
					description = <"">
				>
				["at0037"] = <
					text = <"Liver histology: predominantly lymphoplasmacytic infiltrate">
					description = <"">
				>
				["at0040"] = <
					text = <"Liver histology: rosetting of liver cells">
					description = <"If no interface hepatitis, not predominantly lymphoplasmacytic infiltrate, and no rosetting of liver cells, then subtract 5 points.">
				>
				["at0043"] = <
					text = <"Liver histology: biliary changes">
					description = <"Bile duct changes typical of primary biliary cirrhosis or primary scleroisng cholangitis, and/or periportal ductal reaction with copper-associated protein accumulation.">
				>
				["at0046"] = <
					text = <"Liver histology: other changes">
					description = <"Any other prominent features suggesting different etiology.">
				>
				["at0049"] = <
					text = <"Other autoimmune disease(s)">
					description = <"In patient or 1st degree relatives.">
				>
				["at0052"] = <
					text = <"Response to therapy">
					description = <"">
				>
				["at0053"] = <
					text = <"Complete">
					description = <"">
				>
				["at0054"] = <
					text = <"Relapse">
					description = <"">
				>
				["at0055"] = <
					text = <"Total score">
					description = <"The total sum of each component parameter for the revised original score score for AIH.">
				>
				["at0056"] = <
					text = <"Optional additional parameters">
					description = <"Assign points only in patients seronegative for ANA, SMA and LKM-1.">
				>
				["at0057"] = <
					text = <"None of the above">
					description = <"">
				>
				["at0058"] = <
					text = <"HLA-DR3 or HLA-DR4">
					description = <"">
				>
				["at0059"] = <
					text = <"Seropositivity for other defined autoantibodies">
					description = <"e.g. pANCA, anti-LCl, anti-SLA, anti-ASGPR, anti-LP and anti-sulfatide. ">
				>
				["at0060"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0062"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
	>
