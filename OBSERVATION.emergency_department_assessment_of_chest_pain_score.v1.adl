archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.emergency_department_assessment_of_chest_pain_score.v1

concept
	[at0000]	-- Emergency Department Assessment of Chest Pain Score
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["sv"] = <
			language = <[ISO_639-1::sv]>
			author = <
				["name"] = <"Dennis Forslund">
				["email"] = <"models@cambiocds.com">
				["organisation"] = <"Cambio Healthcare Systems">
			>
		>
	>
description
	original_author = <
		["name"] = <"Syeeda S Farruque">
		["email"] = <"models@cambiocds.com">
		["organisation"] = <"Cambio Healthcare Systems">
		["date"] = <"2017-07-30">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Pain Score (EDACS) provides a method to identify patients with a low risk of a major cardiac adverse event or possible acute coronary syndrom (ACS) to ascertain whether they might be suitable for early discharge.">
			use = <"This score only applies to patients: (1) ≥18 years old with normal vital signs; (2) Chest pain consistent with Acure Coronary Syndrome (ACS); (3) No ongoing chest pain or crescendo angina.

There are various variables that register a score, such as Age, Sex, known CAD (Coronary Artery Disease) or 3 or more risk factors and a number of symptoms and signs. There are 2 additional questions that help to stratify the risk between low and not low which adds to the EDAC score and is called the EDACS-ADP score. This score uses the EDACS score and the variables of EKG findings and whether 0hr and 2hr Troponin are negative or not.

Low Risk Cohort:

EDACS < 16 and
If EKG shows no new ischemia and 0h and 2h troponin both negative

Recommendation: safe for discharge to early outpatient follow-up investigation (or proceed to earlier inpatient testing).

 Not Low Risk Cohort:

EDACS ≥16 or EKG shows new ischemia 0h or 2h troponin positive

Recommendation: Proceed with usual care with further observation and delayed troponin.">
			keywords = <"chest pain", "adverse cardiac event", "acute coronary syndrome">
			misuse = <"Not to be used with patients below 18 years of age or with chest pain not consistent with ACS or if there is ongoing chest pain or crescendo angina.">
			copyright = <"© Cambio Healthcare Systems">
		>
		["sv"] = <
			language = <[ISO_639-1::sv]>
			purpose = <"Emergency Department Assessment of Chest Pain Score (EDACS) är en metod för att identifiera individer med låg risk för akut allvarlig hjärtsjukdom, inklusive akut koronart syndrom, i syfte att i kombination med EDACS-ADP (Accelerated Diagnostic Protocol) bedöma vilka som säkert kan skrivas ut för poliklinisk uppföljning.">
			use = <"Detta instrument är endast tillämpbart på patientkategorierna 1) ≥18 års ålder med normala vitalparametrar, 2) bröstsmärta förenligt med akut koronärt syndrom, 3) ingen pågående bröstsmärta eller instabil angina. 

Poängsumman baseras på flertalet faktorer såsom ålder, kön, känd kranskärlssjukdom samt förekomst av ett flertal olika aktuella symtom och kliniska fynd. Två ytterligare frågor används för riskstratifiering och adderas till EDACS Score; dessa går under kategorin EDACS-ADP, och baseras på EKG-fynd och troponinprover,

Resultatet tolkas enligt:

* Låg risk
EDACS < 16 och
Ingen nytillkommen ischemi på EKG samt negativa troponinprover vid 0h och 2h

Rekommendation: säkert att skriva ut med poliklinisk uppföljning (alternativt att gå vidare med tidig provtagning på sjukhuset).

* Allvarlig akut hjärtsjukdom kan ej uteslutas
EDACS ≥16, eller
Nytillkommen ischemi på EKG/positivt troponinprov vid 0h och 2h

Rekommendation: gå vidare i enlighet med sjukhusets protokoll för vidare observation och troponinprovtagning">
			keywords = <"bröstsmärta", "akut koronart syndrom", "EDACS">
			misuse = <"Ej tillämpbar på patienter under 18 års ålder, patienter med bröstsmärta ej förenlig med akut koronart syndrom, eller vid pågående bröstsmärta eller instabil angina.">
			copyright = <"© Cambio Healthcare Systems">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <>
	other_details = <
		["references"] = <"Ref. 1: Than M, et al. Emerg Med Australas. Development and validation of the Emergency Department Assessment of Chest pain Score and 2 h accelerated diagnostic protocol. 2014 Feb;26(1):34-44. doi: 10.1111/1742-6723.12164.">
		["MD5-CAM-1.0.1"] = <"90C613F5E2BFE3D38944C1B94C044E3B">
	>

definition
	OBSERVATION[at0000] matches {	-- Emergency Department Assessment of Chest Pain Score
		data matches {
			HISTORY[at0001] matches {	-- *Event Series(en)
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- *Any event(en)
						data matches {
							ITEM_TREE[at0003] matches {	-- *Tree(en)
								items cardinality matches {0..*; unordered} matches {
									CLUSTER[at0006] occurrences matches {0..1} matches {	-- Kliniska karakteristika
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0005] occurrences matches {0..1} matches {	-- Ålderskategori
												value matches {
													0|[local::at0026], 	-- N/A
													2|[local::at0027], 	-- 18–45 år
													4|[local::at0028], 	-- 46–50 år
													6|[local::at0029], 	-- 51-55 år
													8|[local::at0030], 	-- 56-60 år
													10|[local::at0031], 	-- 61-65 år
													12|[local::at0032], 	-- 66-70 år
													14|[local::at0033], 	-- 71-75 år
													16|[local::at0034], 	-- 76-80 år
													18|[local::at0035], 	-- 81-85 år
													20|[local::at0036]  	-- 86+ år
												}
											}
											ELEMENT[at0004] occurrences matches {0..1} matches {	-- Kön
												value matches {
													0|[local::at0022], 	-- Kvinna
													6|[local::at0023]  	-- Man
												}
											}
											ELEMENT[at0007] occurrences matches {0..1} matches {	-- Känd kranskärlssjukdom eller >= 3 riskfaktorer
												value matches {
													0|[local::at0024], 	-- Nej
													4|[local::at0025]  	-- Ja
												}
											}
											CLUSTER[at0010] occurrences matches {0..1} matches {	-- Symtom och fynd
												items cardinality matches {1..*; unordered} matches {
													ELEMENT[at0008] occurrences matches {0..1} matches {	-- Diafores
														value matches {
															0|[local::at0020], 	-- Nej
															3|[local::at0021]  	-- Ja
														}
													}
													ELEMENT[at0011] occurrences matches {0..1} matches {	-- Smärtutstrålning till arm, axel, nacke eller käke
														value matches {
															0|[local::at0018], 	-- Nej
															5|[local::at0019]  	-- Ja
														}
													}
													ELEMENT[at0013] occurrences matches {0..1} matches {	-- Smärtdebut eller försämring i samband med inspiration
														value matches {
															-4|[local::at0017], 	-- Ja
															0|[local::at0016]  	-- Nej
														}
													}
													ELEMENT[at0012] occurrences matches {0..1} matches {	-- Smärtan reproducerbar med palpation
														value matches {
															-6|[local::at0015], 	-- Ja
															0|[local::at0014]  	-- Nej
														}
													}
												}
											}
										}
									}
									CLUSTER[at0037] occurrences matches {0..1} matches {	-- EDACS-ADP
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0038] occurrences matches {0..1} matches {	-- Nytillkommen ischemi på EKG?
												value matches {
													0|[local::at0044], 	-- Nej
													1|[local::at0045]  	-- Ja
												}
											}
											ELEMENT[at0039] occurrences matches {0..1} matches {	-- Negativt troponin vid 0h och 2h?
												value matches {
													0|[local::at0040], 	-- Negativt troponin vid 0h och 2h
													1|[local::at0041]  	-- Positivt troponin vid 0h eller 2h
												}
											}
										}
									}
									ELEMENT[at0042] occurrences matches {0..1} matches {	-- Resultat
										value matches {
											DV_COUNT matches {
												magnitude matches {|-10..38|}
											}
										}
									}
									ELEMENT[at0043] occurrences matches {0..1} matches {	-- Kommentar
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Emergency department assessment of chest pain score">
					description = <"Emergency Department Assessment of Chest Pain Score (EDACS) provides a method to identify patients with a low risk of a major cardiac adverse event">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"*">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Sex score">
					description = <"Scores related to gender">
				>
				["at0005"] = <
					text = <"Age score">
					description = <"Scores related to age">
				>
				["at0006"] = <
					text = <"Clinical characteristics">
					description = <"*">
				>
				["at0007"] = <
					text = <"Known CAD or >= 3 risk factors">
					description = <"Only applicable if the patient is 18-50 years old. Than et. al. define coronary artery disease (CAD) as “previous acute myocardial infarction, coronary artery bypass graft or percutaneous intervention.”
Risk factors: family history of premature CAD, dislipidaemia, diabetes, hypertension, current smoker.">
				>
				["at0008"] = <
					text = <"Diaphoresis">
					description = <"*">
				>
				["at0010"] = <
					text = <"Symptoms and Signs">
					description = <"*">
				>
				["at0011"] = <
					text = <"Pain radiates to arm, shoulder, neck or jaw">
					description = <"*">
				>
				["at0012"] = <
					text = <"Pain is reproduced by palpation">
					description = <"*">
				>
				["at0013"] = <
					text = <"Pain occurred or worsened with inspiration">
					description = <"*">
				>
				["at0014"] = <
					text = <"No">
					description = <"*">
				>
				["at0015"] = <
					text = <"Yes">
					description = <"*">
				>
				["at0016"] = <
					text = <"No">
					description = <"*">
				>
				["at0017"] = <
					text = <"Yes">
					description = <"*">
				>
				["at0018"] = <
					text = <"No">
					description = <"*">
				>
				["at0019"] = <
					text = <"Yes">
					description = <"*">
				>
				["at0020"] = <
					text = <"No">
					description = <"*">
				>
				["at0021"] = <
					text = <"Yes">
					description = <"*">
				>
				["at0022"] = <
					text = <"Female">
					description = <"*">
				>
				["at0023"] = <
					text = <"Male">
					description = <"*">
				>
				["at0024"] = <
					text = <"No">
					description = <"*">
				>
				["at0025"] = <
					text = <"Yes">
					description = <"*">
				>
				["at0026"] = <
					text = <"N/A">
					description = <"*">
				>
				["at0027"] = <
					text = <"18–45 years">
					description = <"*">
				>
				["at0028"] = <
					text = <"46–50 years">
					description = <"*">
				>
				["at0029"] = <
					text = <"51-55 years">
					description = <"*">
				>
				["at0030"] = <
					text = <"56-60 years">
					description = <"*">
				>
				["at0031"] = <
					text = <"61-65 years">
					description = <"*">
				>
				["at0032"] = <
					text = <"66-70 years">
					description = <"*">
				>
				["at0033"] = <
					text = <"71-75 years">
					description = <"*">
				>
				["at0034"] = <
					text = <"76-80 years">
					description = <"*">
				>
				["at0035"] = <
					text = <"81-85 years">
					description = <"*">
				>
				["at0036"] = <
					text = <"86+ years">
					description = <"*">
				>
				["at0037"] = <
					text = <"EDACS-ADP">
					description = <"*">
				>
				["at0038"] = <
					text = <"Was there new ischemia on ECG?">
					description = <"Was there new ischemia on ECG?">
				>
				["at0039"] = <
					text = <"Was both 0hr and 2hr troponin negative?">
					description = <"These EDACS-ADP questions help to evaluate for the purposes of the EDACS - Accelerated Diagnostic Protocol for fast evaluation. These questions must be combined with the EDACS score for the evaluation to be completed. These questions  are not added to the score for EDACS however.">
				>
				["at0040"] = <
					text = <"Both 0hr and 2hr Troponin was negative">
					description = <"*">
				>
				["at0041"] = <
					text = <"Either 0hr or 2hr Troponin was positive">
					description = <"*">
				>
				["at0042"] = <
					text = <"Total score">
					description = <"Sum of the individual EDACS scores (not including the ADP section)">
				>
				["at0043"] = <
					text = <"Comments">
					description = <"Comments regarding the score">
				>
				["at0044"] = <
					text = <"No">
					description = <"*">
				>
				["at0045"] = <
					text = <"Yes">
					description = <"*">
				>
			>
		>
		["sv"] = <
			items = <
				["at0000"] = <
					text = <"Emergency Department Assessment of Chest Pain Score">
					description = <"Emergency Department Assessment of Chest Pain Score (EDACS) är en metod för att identifiera individer med låg risk för akut allvarlig hjärtsjukdom.">
				>
				["at0001"] = <
					text = <"*Event Series(en)">
					description = <"*@ internal @(en)">
				>
				["at0002"] = <
					text = <"*Any event(en)">
					description = <"**(en)">
				>
				["at0003"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0004"] = <
					text = <"Kön">
					description = <"Poäng relaterad till kön.">
				>
				["at0005"] = <
					text = <"Ålderskategori">
					description = <"Poäng för ålder.">
				>
				["at0006"] = <
					text = <"Kliniska karakteristika">
					description = <"">
				>
				["at0007"] = <
					text = <"Känd kranskärlssjukdom eller >= 3 riskfaktorer">
					description = <"Endast tillämpbar om patienten är 18-50 år gammal. Than et. al. (ref 1) definierar kranskärlssjukdom som “tidigare infarkt, kranskärlsoperation eller perkutan intervention”.
Riskfaktorer: hereditet för förtida kranskärlssjukdom, dyslipidemi, diabetes, hypertoni, rökare.">
				>
				["at0008"] = <
					text = <"Diafores">
					description = <"*">
				>
				["at0010"] = <
					text = <"Symtom och fynd">
					description = <"*">
				>
				["at0011"] = <
					text = <"Smärtutstrålning till arm, axel, nacke eller käke">
					description = <"*">
				>
				["at0012"] = <
					text = <"Smärtan reproducerbar med palpation">
					description = <"*">
				>
				["at0013"] = <
					text = <"Smärtdebut eller försämring i samband med inspiration">
					description = <"">
				>
				["at0014"] = <
					text = <"Nej">
					description = <"*">
				>
				["at0015"] = <
					text = <"Ja">
					description = <"*">
				>
				["at0016"] = <
					text = <"Nej">
					description = <"">
				>
				["at0017"] = <
					text = <"Ja">
					description = <"">
				>
				["at0018"] = <
					text = <"Nej">
					description = <"*">
				>
				["at0019"] = <
					text = <"Ja">
					description = <"*">
				>
				["at0020"] = <
					text = <"Nej">
					description = <"*">
				>
				["at0021"] = <
					text = <"Ja">
					description = <"*">
				>
				["at0022"] = <
					text = <"Kvinna">
					description = <"*">
				>
				["at0023"] = <
					text = <"Man">
					description = <"*">
				>
				["at0024"] = <
					text = <"Nej">
					description = <"">
				>
				["at0025"] = <
					text = <"Ja">
					description = <"">
				>
				["at0026"] = <
					text = <"N/A">
					description = <"">
				>
				["at0027"] = <
					text = <"18–45 år">
					description = <"">
				>
				["at0028"] = <
					text = <"46–50 år">
					description = <"">
				>
				["at0029"] = <
					text = <"51-55 år">
					description = <"">
				>
				["at0030"] = <
					text = <"56-60 år">
					description = <"">
				>
				["at0031"] = <
					text = <"61-65 år">
					description = <"">
				>
				["at0032"] = <
					text = <"66-70 år">
					description = <"">
				>
				["at0033"] = <
					text = <"71-75 år">
					description = <"">
				>
				["at0034"] = <
					text = <"76-80 år">
					description = <"">
				>
				["at0035"] = <
					text = <"81-85 år">
					description = <"">
				>
				["at0036"] = <
					text = <"86+ år">
					description = <"">
				>
				["at0037"] = <
					text = <"EDACS-ADP">
					description = <"EDACS-ADP används för att utvärdering i enlighet med EDACS - Accelerated Diagnostic Protocol för snabb bedömning. Måste kombineras med EDACS Score för komplett bedömning, men är separata.">
				>
				["at0038"] = <
					text = <"Nytillkommen ischemi på EKG?">
					description = <"EDACS-ADP används för att utvärdering i enlighet med EDACS - Accelerated Diagnostic Protocol för snabb bedömning. Måste kombineras med EDACS Score för komplett bedömning, men är separata.">
				>
				["at0039"] = <
					text = <"Negativt troponin vid 0h och 2h?">
					description = <"EDACS-ADP används för att utvärdering i enlighet med EDACS - Accelerated Diagnostic Protocol för snabb bedömning. Måste kombineras med EDACS Score för komplett bedömning, men är separata.">
				>
				["at0040"] = <
					text = <"Negativt troponin vid 0h och 2h">
					description = <"*">
				>
				["at0041"] = <
					text = <"Positivt troponin vid 0h eller 2h">
					description = <"*">
				>
				["at0042"] = <
					text = <"Resultat">
					description = <"Summan av samtliga faktorer. EDACS-ADP inkluderas ej.">
				>
				["at0043"] = <
					text = <"Kommentar">
					description = <"Ev. kommentar kring instrumentet.">
				>
				["at0044"] = <
					text = <"Nej">
					description = <"*">
				>
				["at0045"] = <
					text = <"Ja">
					description = <"*">
				>
			>
		>
	>
